apply plugin: 'application'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
    }
}

description = "Dummy google client and server over gRPC"

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.grpc:grpc-all:1.0.1'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.1.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }

    generatedFilesBaseDir = "$projectDir/src/generated"
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir '../proto'
        }
    }
}

startScripts.enabled = false

task searchClient(type: CreateStartScripts) {
    mainClassName = "com.grpc.search.clients.Starter"
    applicationName = "grpc-google-client"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

task backend(type: CreateStartScripts) {
    mainClassName = "com.grpc.search.backend.Backend"
    applicationName = "grpc-google-backend"
    outputDir = new File(project.buildDir, 'tmp')
    classpath = jar.outputs.files + project.configurations.runtime
}

applicationDistribution.into("bin") {
    from(searchClient)
    from(backend)
    fileMode = 0755
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
        sourceDirs += file("${projectDir}/src/generated/main/java");
    }
}
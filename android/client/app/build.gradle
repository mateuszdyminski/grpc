apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "com.grpc"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'HardcodedText'
        textReport true
        textOutput "stdout"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:+'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'io.grpc:grpc-protobuf-lite:1.0.1'
    compile 'io.grpc:grpc-okhttp:1.0.1'
    compile 'io.grpc:grpc-stub:1.0.1'
    compile 'javax.annotation:javax.annotation-api:1.2'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'
    testCompile 'junit:junit:4.12'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-beta-3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // Javanano is installed by default, but needs to be removed to use protobuf lite..
                remove javanano
                java {
                    // Options added to --java_out
                    option 'lite'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }

    generatedFilesBaseDir = "$projectDir/src/generated"
}

apply plugin: 'idea'

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
        sourceDirs += file("${projectDir}/src/generated/main/java");
    }
}